# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Adam Hani Schakaki <krzd@krzd.net>

pkgname=caribou
pkgver=0.4.4
pkgrel=2
pkgdesc="A text entry and UI navigation application being developed as an alternative to the Gnome On-screen Keyboard."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Caribou"
license=('LGPL')
depends=('at-spi2-atk' 'pyatspi' 'dbus-python' 'python2-gobject' 'gtk3' 'libxklavier' 'libgee' 'clutter' 'dconf')
makedepends=('intltool' 'docbook-xsl')
install=caribou.install
options=(!libtool !emptydirs)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.4/$pkgname-$pkgver.tar.xz
        fix-crash.patch)
sha256sums=('823c53afec88d43f06ddce63e0d1aac79d783f498502a0617d8be0cffc6a7de2'
            '813229104fcb15ae5d3c197362fa7474a4963eb31d241d4f5354f7000d03f9e1')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-crash.patch
  sed -i s'|#!/usr/bin/python|#!/usr/bin/python2|'g tools/{fix_gir,make_schema}.py
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/caribou \
    --disable-gtk2-module \
    --disable-static \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:

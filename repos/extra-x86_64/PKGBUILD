# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=enlightenment
pkgver=0.16.8.13
_themever=0.16.8.0.2
pkgrel=1
pkgdesc="A fast, flexible, and very extensible Window Manager"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('freetype2' 'libxinerama' 'libxft' 'libsm' 'libxxf86vm' 'esd' 'imlib2' 'perl' 'libxcomposite' 'libxdamage' 'libxrandr')
makedepends=('xbitmaps')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/e16-${pkgver}.tar.gz \
        http://downloads.sourceforge.net/enlightenment/e16-themes-${_themever}.tar.gz)
md5sums=('50e3e0d128b0f1046c95e2820a1bc841' '20fbaff803c3decdf3ac7c363b37c42e')
sha1sums=('9bcbb42add4447624db3179a79476f0f858974bb' '44dd62c5a476561c7ab4382ddd0250a5a0902065')

build() {
  cd ${startdir}/src/e16-${pkgver}
  sed -i 's|xsessiondir = $(datadir)/xsessions|xsessiondir = /etc/X11/sessions|' misc/Makefile.in
  ./configure --prefix=/usr --enable-fsstd --enable-sound --sysconfdir=/etc
  make || return 1
  make DESTDIR=${startdir}/pkg install
  install -D -m755 ${startdir}/pkg/usr/share/e16/misc/Xclients.e16-gnome.sh ${startdir}/pkg/usr/bin/e16-gnome
  install -D -m755 ${startdir}/pkg/usr/share/e16/misc/Xclients.e16-kde.sh ${startdir}/pkg/usr/bin/e16-kde
  install -D -m644 COPYING ${startdir}/pkg/usr/share/licenses/${pkgname}/COPYING
  install -D -m644 man/e16.1 ${startdir}/pkg/usr/share/man/man1/e16.1

  cd ${startdir}/src/e16-themes-${_themever}
  ./configure --prefix=/usr --enable-fsstd
  make DESTDIR=${startdir}/pkg install
 
  chown -R root:root ${startdir}/pkg/usr/share/*
}

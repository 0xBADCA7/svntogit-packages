# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=nawk
pkgver=20110810
pkgrel=1
pkgdesc="The one, true implementation of the AWK"
url="http://cm.bell-labs.com/who/bwk/"
license=('MIT')
arch=('i686' 'x86_64')
options=('!makeflags')
depends=('glibc')
provides=('awk')
source=("http://www.cs.princeton.edu/~bwk/btl.mirror/awk.tar.gz")
md5sums=('a7062ecade93dc4cb39190e48db9a51d')

build() {
  CPPFLAGS=-DHAS_ISBLANK make
  sed -n '/Copyright/,/THIS SOFTWARE\./p' README > LICENSE
}

package() {
  install -Dm755 a.out "$pkgdir/usr/bin/nawk"
  install -Dm644 awk.1 "$pkgdir/usr/share/man/man1/nawk.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/nawk/LICENSE"
}

# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyqtwebengine
pkgname=(python-pyqtwebengine python2-pyqtwebengine)
pkgver=5.13.2
pkgrel=4
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqtwebengine/intro"
license=(GPL)
makedepends=(sip sip5 pyqt-builder qt5-webengine python-pyqt5 python2-pyqt5)
source=("https://www.riverbankcomputing.com/static/Downloads/PyQtWebEngine/$pkgver/PyQtWebEngine-$pkgver.tar.gz")
sha256sums=('4264911b5847c75721d8c9c30af92e58a216bd25ceef37f7abf921005c1d45a9')

prepare() {
  cp -a PyQtWebEngine-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PyQtWebEngine-$pkgver
  sip-build \
    --no-make
  cd build
  make

  cd "$srcdir"/PyQtWebEngine-$pkgver-py2
  python2 configure.py \
    --sip='/usr/bin/sip'
  make
}

package_python-pyqtwebengine() {
  pkgdesc="Python 3 bindings for QtWebEngine"
  depends=(python-pyqt5 qt5-webengine)

  cd PyQtWebEngine-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install -j1
}

package_python2-pyqtwebengine() {
  pkgdesc="Python 2 bindings for QtWebEngine"
  depends=(python2-pyqt5 qt5-webengine)
  conflicts=(pyqtwebengine-common)

  cd PyQtWebEngine-$pkgver-py2
  make INSTALL_ROOT="$pkgdir" install -j1
}

# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sip5
pkgver=5.0.0
pkgrel=3
arch=(x86_64)
pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
url='https://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
depends=(python-setuptools python-toml)
source=("https://www.riverbankcomputing.com/static/Downloads/sip/$pkgver/sip-$pkgver.tar.gz"
         sip5-destdir.patch)
sha256sums=('c034d427f52584d63277dd7df4d16d00c5df0b5cb838e0c256865b854b7e074b'
            '337daf8721b14066160afaae85e2b3ad880ad6d016407a754e3f11331c4b65eb')

prepare() {
  cd sip-$pkgver
  patch -p1 -i ../sip5-destdir.patch # Honor DESTDIR in make install https://www.riverbankcomputing.com/pipermail/pyqt/2019-December/042390.html
}

build() {
  cd sip-$pkgver
  python setup.py build
}

package() {
  cd sip-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}

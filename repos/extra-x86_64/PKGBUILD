# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Paul Ezvan <paul@ezvan.fr>

pkgname=terminology
pkgver=1.3.2
pkgrel=1
pkgdesc="EFL based terminal emulator"
arch=('x86_64')
url="https://www.enlightenment.org/about-terminology"
license=('BSD')
depends=('efl' 'hicolor-icon-theme')
makedepends=('mesa' 'meson')
#source=("https://download.enlightenment.org/rel/apps/${pkgname}/${pkgname}-${pkgver}.tar.xz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/billiob/terminology/archive/v${pkgver}.tar.gz")
sha256sums=('7b5b94e173b5414e343483ab4a418796216104c8e9c098e988e2583638ab8825')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -fvisibility=hidden"
  meson builddir --prefix=/usr
  ninja -C builddir
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}" ninja -C builddir install

  # install license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

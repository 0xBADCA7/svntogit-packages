# Maintainer:
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=icewm
pkgver=1.5.3
pkgrel=1
pkgdesc="A Window Manager designed for speed, usability, and consistency"
arch=('x86_64')
url="https://ice-wm.org/"
license=('LGPL')
depends=('libxrandr' 'libsm' 'libxinerama' 'libsndfile' 'libao' 'fribidi' 'libxft' 'gdk-pixbuf2')
makedepends=('xorg-mkfontdir' 'asciidoctor' 'librsvg')
optdepends=('perl: for icewm-menu-xrandr')
source=(https://github.com/ice-wm/icewm/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('98d0b59a75c86aa4272b5855f602ae8692ff6b748a703b8ee54b2abac45e5afb')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

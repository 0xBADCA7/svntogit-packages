# Maintainer:
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=icewm
pkgver=1.5.2
pkgrel=1
pkgdesc="A Window Manager designed for speed, usability, and consistency"
arch=('x86_64')
url="https://ice-wm.org/"
license=('LGPL')
depends=('libxrandr' 'libxft' 'libsm' 'libxinerama' 'gdk-pixbuf2' 'fribidi' 'librsvg')
makedepends=('xorg-mkfontdir' 'asciidoctor')
source=(https://github.com/ice-wm/icewm/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('0c7f827ae774eb93ba66839fef0cd75309f4459ee688e7b6e04ffec291a0799b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-shaped-decorations --enable-gradients
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Enlightenment Developers <enlightenment-devel@enlightenment.org>>

pkgname=enlightenment
pkgver=0.21.1
pkgrel=1
pkgdesc="Enlightenment window manager"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('efl' 'xcb-util-keysyms' 'hicolor-icon-theme' 'pixman' 'mesa'
         'desktop-file-utils' 'udisks2' 'ttf-font' 'bluez-libs')
optdepends=('connman: network module'
            'acpid: power events on laptop lid close'
	        'geoip-database: geolocation module')
provides=('notification-daemon')
backup=('etc/enlightenment/sysactions.conf'
        'etc/xdg/menus/e-applications.menu')
source=("http://download.enlightenment.org/rel/apps/${pkgname}/$pkgname-$pkgver.tar.gz"
        'e_alert.patch')
sha1sums=('b52cf961a15e89a062499f344713d8375f7891db'
          '23e305c9f0eaa9d22963f2ac79c4d87a29fb422b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # upstream build fix: define EFL_BETA_API_SUPPORT before any include
  patch -Np1 -i ${srcdir}/e_alert.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="$CFLAGS -fvisibility=hidden"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-xwayland --enable-wayland \
    --disable-wl-weekeyboard

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="$pkgdir" install

  # install LICENSE
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=flashplugin
pkgver=24.0.0.194
pkgrel=1
pkgdesc='Adobe Flash Player'
arch=('i686' 'x86_64')
url='https://get.adobe.com/flashplayer/'
license=('custom')
depends=('mozilla-common' 'libxt' 'libxpm' 'gtk2' 'nss' 'curl' 'hicolor-icon-theme')
makedepends_x86_64=('chrpath')
optdepends=('libvdpau: GPU acceleration on Nvidia card')
provides=('flashplayer')
options=('!strip')
backup=('etc/adobe/mms.cfg')
install=flashplugin.install
source=(mms.cfg)
source_i686=(flash_player_npapi_linux_$pkgver.i386.tar.gz::https://fpdownload.adobe.com/get/flashplayer/pdc/$pkgver/flash_player_npapi_linux.i386.tar.gz)
source_x86_64=(flash_player_npapi_linux_$pkgver.x86_64.tar.gz::https://fpdownload.adobe.com/get/flashplayer/pdc/$pkgver/flash_player_npapi_linux.x86_64.tar.gz)
sha256sums=('27dad99feecfd7152305bd86e8149e8c6482b7dc787ccd205c05d1dc13aafead')
sha256sums_i686=('f461cbabc965cb5fa6d2271259e166340e8b20833f6a9ce60a81a04021e72ed3')
sha256sums_x86_64=('fd814f9fdf28d366f49ea308e7e45b3f97df2be4a463d26312e9258fe544312f')

package() {
  install -D libflashplayer.so "$pkgdir/usr/lib/mozilla/plugins/libflashplayer.so"
  install -D usr/bin/flash-player-properties "$pkgdir/usr/bin/flash-player-properties"
  if [[ $CARCH == x86_64 ]]; then
    install -D usr/lib64/kde4/kcm_adobe_flash_player.so "$pkgdir/usr/lib/kde4/kcm_adobe_flash_player.so"
    chrpath -d "$pkgdir/usr/lib/kde4/kcm_adobe_flash_player.so"
  else
    install -D usr/lib/kde4/kcm_adobe_flash_player.so "$pkgdir/usr/lib/kde4/kcm_adobe_flash_player.so"
  fi

  for size in 16x16 22x22 24x24 32x32 48x48; do
    install -Dm644 usr/share/icons/hicolor/$size/apps/flash-player-properties.png \
      "$pkgdir/usr/share/icons/hicolor/$size/apps/flash-player-properties.png"
  done
  install -Dm644 usr/share/applications/flash-player-properties.desktop "$pkgdir/usr/share/applications/flash-player-properties.desktop"
  install -Dm644 usr/share/kde4/services/kcm_adobe_flash_player.desktop "$pkgdir/usr/share/kde4/services/kcm_adobe_flash_player.desktop"

  install -Dm644 mms.cfg "$pkgdir/etc/adobe/mms.cfg"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" license.pdf LGPL/*.txt
}

# vim:set ts=2 sw=2 et:

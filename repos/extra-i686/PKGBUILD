# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=enlightenment
pkgver=0.16.8.15
_themever=0.16.8.0.3
pkgrel=1
pkgdesc="A fast, flexible, and very extensible Window Manager"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('freetype2' 'libxinerama' 'libxft' 'libsm' 'libxxf86vm' 'esound' 'imlib2' 'perl' 'libxcomposite' 'libxdamage' 'libxrandr')
makedepends=('xbitmaps')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/e16-${pkgver}.tar.gz \
        http://downloads.sourceforge.net/enlightenment/e16-themes-${_themever}.tar.gz)
md5sums=('65c753282965985a11447bf7a8bfabab' '26b82f5383e26cd3d4b883cb78cd6c21')
sha1sums=('4bcd91edde5b9ab2486af0814de8d3592f669d70' 'ac94ad389760081570fe62717270794e060678a1')

build() {
  cd ${srcdir}/e16-${pkgver}
  sed -i 's|xsessiondir = $(datadir)/xsessions|xsessiondir = /etc/X11/sessions|' misc/Makefile.in || return 1
  ./configure --prefix=/usr --enable-fsstd --enable-sound --sysconfdir=/etc || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m755 ${pkgdir}/usr/share/e16/misc/Xclients.e16-gnome.sh ${pkgdir}/usr/bin/e16-gnome || return 1
  install -D -m755 ${pkgdir}/usr/share/e16/misc/Xclients.e16-kde.sh ${pkgdir}/usr/bin/e16-kde || return 1
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING || return 1
  install -D -m644 man/e16.1 ${pkgdir}/usr/share/man/man1/e16.1 || return 1

  cd ${srcdir}/e16-themes-${_themever}
  ./configure --prefix=/usr --enable-fsstd || return 1
  make DESTDIR=${pkgdir} install || return 1
 
  chown -R root:root ${pkgdir}/usr/share/* || return 1
}

# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Adam Hani Schakaki <krzd@krzd.net>

pkgname=caribou
pkgver=0.3.91
pkgrel=1
pkgdesc="A text entry and UI navigation application being developed as an alternative to the Gnome On-screen Keyboard."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Caribou"
license=('LGPL')
depends=('pyatspi' 'dbus-python' 'python2-gobject' 'gtk3' 'libxklavier' 'libgee' 'dconf')
makedepends=('gnome-doc-utils' 'intltool' 'clutter')
install=caribou.install
options=(!libtool !emptydirs)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.3/$pkgname-$pkgver.tar.xz)
sha256sums=('4fe2f493d21fd3fcbdd846f26a5b308f7af62db55f6ac4ae6e91513eb7316bca')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i s'|#!/usr/bin/python|#!/usr/bin/python2|'g tools/{fix_gir,make_schema}.py
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/caribou \
    --disable-gtk2-module \
    --disable-static \
    --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:

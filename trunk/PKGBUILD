# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Enlightenment Developers <enlightenment-devel@enlightenment.org>>

pkgname=enlightenment
pkgver=0.22.2
pkgrel=2
pkgdesc="Enlightenment window manager"
arch=('x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('efl' 'xcb-util-keysyms' 'hicolor-icon-theme' 'pixman' 'mesa'
         'desktop-file-utils' 'udisks2' 'ttf-font' 'bluez-libs' 'pam')
optdepends=('connman: network module'
            'acpid: power events on laptop lid close'
	        'geoip-database: geolocation module'
            'xorg-server-xwayland: xwayland support')
makedepends=('xorg-server-xwayland')
provides=('notification-daemon')
backup=('etc/enlightenment/sysactions.conf'
        'etc/xdg/menus/e-applications.menu')
source=("https://download.enlightenment.org/rel/apps/${pkgname}/$pkgname-$pkgver.tar.xz"
        "upstream_pam_1.patch" "upstream_pam_2.patch")
sha512sums=('718dafcb72b438cabe49de23495539d1417ac80ae239d2c8296aaa9eb5cdf608521129386dea63bcf281c9ddce7efeec540888d984559fbd196d925e2bab1b78'
            '6bac54795893a9280af98d4297e87d5408c726b5bced82574c2166e6f789eb908514cf2c95ca6cc2c14ee8c461403e5517e0cc72f5e9df67781e4b26c3d6a3cb'
            '2747e491bff02cb11eec1797abd7851f79feca93a3162f838c68f0a388332c9e95ff427edf68eccf88ad333d2c3250241612dbdcf90c8e86de09ffd250eb8dad')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/upstream_pam_1.patch || return 1
  patch -Np1 -i ${srcdir}/upstream_pam_2.patch || return 1
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="$CFLAGS -fvisibility=hidden"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-xwayland --enable-wayland \
    --disable-wl-weekeyboard

  make
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="$pkgdir" install

  # install LICENSE
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

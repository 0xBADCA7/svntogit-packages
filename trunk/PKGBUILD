# $Id: PKGBUILD 199714 2013-11-15 10:46:56Z ronald $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=efl
pkgver=1.8.2
pkgrel=1
pkgdesc="Enlightenment Foundation Libraries"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=()
replaces=('ecore' 'e_dbus' 'edje' 'eet' 'eeze' 'efreet' 'eina' 'eio' 'embryo' 
	'emotion' 'ethumb' 'evas')
options=('!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)

build() {
  cd $srcdir/$pkgname-$pkgver

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-posix-threads \
    --disable-debug-threads \
    --enable-inotify \
    --disable-install-examples \
    --disable-doc
  make
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # install license files
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/COPYING
}


# $Id: PKGBUILD 199714 2013-11-15 10:46:56Z ronald $
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Enlightenment Developers <enlightenment-devel@enlightenment.org>

pkgname=('efl' 'efl_doc')
pkgver=1.8.2
pkgrel=1
pkgdesc="Enlightenment Foundation Libraries"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD' 'LGPL2.1' 'GPL2' 'custom')
depends=('bullet' 'libpng' 'libjpeg-turbo' 'gstreamer0.10'
         'gstreamer0.10-base' 'gstreamer0.10-good'
         'gstreamer0.10-bad' 'gstreamer0.10-ugly' 'gstreamer0.10-ffmpeg'
         'zlib' 'lua' 'libtiff' 'openssl' 'util-linux' 'curl' 'dbus'
         'glibc' 'fontconfig' 'freetype2' 'fribidi' 'libpulse'
         'libsndfile' 'systemd' 'libx11' 'libxau' 'libxcomposite'
         'libxdamage' 'libxdmcp' 'libxext' 'libxfixes' 'libxinerama'
         'libxi' 'libxrandr' 'libxrender' 'libxss' 'libxtst'
         'libxcursor' 'libxp' 'libgl' 'giflib' 'libwebp' 'harfbuzz')
makedepends=('doxygen')
replaces=('ecore' 'e_dbus' 'edje' 'eet' 'eeze' 'efreet' 'eina' 'eio' 'embryo' 
	'emotion' 'ethumb' 'evas')
options=('!emptydirs')
source=(http://download.enlightenment.org/rel/libs/${pkgname}/$pkgname-$pkgver.tar.gz)
install=efl.install
sha1sums=('72cbe83cf27cf3bf0f2676fe55b771d1618f892d')


prepare() {
  sed -i 's/env python$/&2/' "${srcdir}/${pkgname}-${pkgver}/src/scripts/eina/eina-bench-cmp"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure --prefix=/usr \
  --disable-static --disable-tslib --enable-fb \
  --enable-xinput22 --enable-multisense --enable-systemd \
  --enable-image-loader-webp --enable-harfbuzz 

  make
}

package_efl(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  # install non-standard license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.BSD" \
	"${pkgdir}/usr/share/licenses/$pkgname/COPYING.BSD"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/licenses/COPYING.SMALL" \ 
        "${pkgdir}/usr/share/licenses/$pkgname/COPYING.SMALL"
}

package_efl_doc() {
  pkgdesc="Documentation for the Enlightenment Foundation Libraries"
  arch=('any')

  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a doc/html "${pkgdir}/usr/share/doc/${pkgname}/html"
  cp -a doc/latex "${pkgdir}/usr/share/doc/${pkgname}/latex"
}


# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Michal Soltys <soltys@ziu.info>

pkgname=arptables
pkgver=0.0.4
pkgrel=4
pkgdesc='ARP filtering utility'
arch=('i686' 'x86_64')
url='http://http://ebtables.netfilter.org/'
depends=('glibc' 'perl' 'bash')
license=('GPL')
backup=("etc/$pkgname.conf")
source=("https://downloads.sourceforge.net/ebtables/$pkgname-v${pkgver//_/-}.tar.gz"
        "$pkgname.systemd"
        "$pkgname.service")
sha256sums=('277985e29ecd93bd759a58242cad0e02ba9d4a6e1b7795235e3b507661bc0049'
            '93585af516b9f5544a88bfca96dd56afa60304a20e94b29c1cae06bfc1b73451'
            'f93b5a750a81f2c1543f21dd9cbb54347d59ceacc7044c88d15690df7c38f78a')

build() {
  cd $pkgname-v${pkgver//_/-}
  make
}

package() {
  pushd $pkgname-v${pkgver//_/-}
  make install \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    LIBDIR=/usr/lib/arptables \
    BINDIR=/usr/bin \
    MANDIR=/usr/share/man \
    INITDIR=/etc/rc.d \
    SYSCONFIGDIR=/etc
  popd
  # systemd
  install -Dm 755 $pkgname.systemd \
    "$pkgdir/usr/lib/systemd/scripts/$pkgname"
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
  # remove upstream rc
  rm -rf "$pkgdir/etc/rc.d"
}

# vim:set ts=2 sw=2 et:
